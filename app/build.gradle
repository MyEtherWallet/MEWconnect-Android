apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.kotlin.android.extensions'

int MAJOR_VERSION = 0
int MINOR_VERSION = 0
int MICRO_VERSION = 1
int BUILD_FOR_TODAY = 1

def secretsPropertiesFile = rootProject.file("secrets.properties");
def secretsProperties = new Properties()
secretsProperties.load(new FileInputStream(secretsPropertiesFile))

androidExtensions {
    experimental = true
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.myetherwallet.mewconnect"
        minSdkVersion 23
        targetSdkVersion 28
        if (MICRO_VERSION == 0) {
            versionName String.format("%d.%d", MAJOR_VERSION, MINOR_VERSION)
        } else {
            versionName String.format("%d.%d.%d", MAJOR_VERSION, MINOR_VERSION, MICRO_VERSION)
        }
        def date = new Date()
        versionCode Integer.valueOf(String.format("%ty%tm%td%02d", date, date, date, BUILD_FOR_TODAY))
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "CONNECT_API_END_POINT", "\"https://connect.mewapi.io\""
        buildConfigField "String", "MEW_API_END_POINT", "\"https://api.myetherwallet.com/\""
        buildConfigField "String", "APICCSWAP_API_END_POINT", "\"https://apiccswap.myetherwallet.com/\""
        buildConfigField "String", "APICCSWAP_API_KEY", "\"" + secretsProperties['apiccswapApiKey'] + "\""
        buildConfigField "String", "APICCSWAP_REFERER", "\"" + secretsProperties['apiccswapReferer'] + "\""
        buildConfigField "String", "HEROKU_API_END_POINT", "\"http://still-waters-52916.herokuapp.com/\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:0.22.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    api 'com.android.support:design:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.github.alexjlockwood:kyrie:0.1.3'
    implementation 'com.google.firebase:firebase-ml-vision:17.0.1'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    kapt "android.arch.lifecycle:compiler:1.1.1"
    kapt "com.google.dagger:dagger-compiler:2.13"
    implementation 'com.google.dagger:dagger:2.13'
    implementation 'javax.annotation:jsr250-api:1.0'
    implementation 'javax.inject:javax.inject:1'
    implementation 'android.arch.persistence.room:runtime:1.1.1'
    kapt 'android.arch.persistence.room:compiler:1.1.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    implementation 'com.nulab-inc:zxcvbn:1.2.5'
    implementation 'com.github.kenglxn.QRGen:android:2.5.0'
    implementation('io.socket:socket.io-client:1.0.0') {
        // excluding org.json which is provided by Android
        exclude group: 'org.json', module: 'json'
    }
    implementation 'com.madgag.spongycastle:prov:1.58.0.0'
    // Add dependencies manually because a bug https://github.com/web3j/web3j/issues/538#issuecomment-398783748 {
    implementation files('libs/crypto-3.3.1-android.jar')
    implementation files('libs/utils-3.3.1-android.jar')
    implementation files('libs/rlp-3.3.1-android.jar')
    implementation files('libs/abi-3.3.1-android.jar')
    implementation files('libs/tuples-3.3.1-android.jar')
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.6'
    implementation 'org.slf4j:slf4j-api:1.8.0-beta2'
    implementation 'io.reactivex:rxjava:1.2.4'
    // }
    implementation 'org.bitcoinj:bitcoinj-core:0.14.7'
    implementation 'org.webrtc:google-webrtc:1.0.20723'
    implementation 'com.github.gnosis.svalinn-kotlin:blockies:v0.5.4'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

kotlin {
    experimental {
        coroutines "enable"
    }
}
